# -- PUBLIC METHODS BELOW HERE ---------------------------------------------------------------------------------------- #
"""
    build(modeltype::Type{MyClassicalHopfieldNetworkModel}, data::NamedTuple) -> MyClassicalHopfieldNetworkModel

Factory method for building a Hopfield network model. 

### Arguments
- `modeltype::Type{MyClassicalHopfieldNetworkModel}`: the type of the model to be built.
- `data::NamedTuple`: a named tuple containing the data for the model.

The named tuple should contain the following fields:
- `memories`: a matrix of memories (each column is a memory).

### Returns
- `model::MyClassicalHopfieldNetworkModel`: the built Hopfield network model with the following fields populated:
    - `W`: the weight matrix.
    - `b`: the bias vector.
    - `energy`: a dictionary of energies for each memory.
"""
function build(modeltype::Type{MyClassicalHopfieldNetworkModel}, data::NamedTuple)::MyClassicalHopfieldNetworkModel

    # initialize -
    model = modeltype();
    linearimagecollection = data.memories;
    number_of_rows, number_of_cols = size(linearimagecollection);
    W = zeros(Float32, number_of_rows, number_of_rows);
    b = zeros(Float32, number_of_rows);
    L = -1.0; # lower bound
    U = 1.0; # upper bound

    # compute the W -
    for j ∈ 1:number_of_cols
        Y = ⊗(linearimagecollection[:,j], linearimagecollection[:,j]); # compute the outer product -
        W += Y; # update the W -
    end
    WN = (1/number_of_cols)*W; # normalize the W (no average)

    # generate a random bias vector -
    for i ∈ 1:number_of_rows
        f = rand();
        b[i] = f*U+(1-f)*L;
    end
    
    # compute the energy dictionary -
    energy = Dict{Int64, Float32}();
    for i ∈ 1:number_of_cols
        energy[i] = _energy(linearimagecollection[:,i], WN, b);
    end

    # add data to the model -
    model.W = WN;
    model.b = b;
    model.energy = energy;

    # return -
    return model;
end

"""
    build(modeltype::Type{MyModernHopfieldNetworkModel}, data::NamedTuple) -> MyModernHopfieldNetworkModel

Factory method for building a modern Hopfield network model.

### Arguments
- `modeltype::Type{MyModernHopfieldNetworkModel}`: the type of the model to be built.
- `data::NamedTuple`: a named tuple containing the data for the model.

The named tuple should contain the following fields:
- `memories`: a matrix of memories (each column is a memory).
- `β`: the beta parameter for the model (inverse temperature).

### Returns
- `model::MyModernHopfieldNetworkModel`: the built modern Hopfield network model with the following fields populated:
    - `β`: the beta parameter.
    - `X`: the collection of memories.
"""
function build(modeltype::Type{MyModernHopfieldNetworkModel}, data::NamedTuple)::MyModernHopfieldNetworkModel

    # initialize -
    model = modeltype();
    linearmemorycollection = data.memories;
    β = data.β; # beta parameter
    
    # add stuff the model -
    model.β = β;
    model.X = linearmemorycollection;

    # return -
    return model;
end
# --- PUBLIC METHODS ABOVE HERE --------------------------------------------------------------------------------------- #